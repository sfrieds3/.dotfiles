# set prompt
autoload -U promptinit && promptinit

# lazy reload git stuff to speed up prompt in large repos
# (from https://github.com/zsh-users/zsh/blob/master/Misc/vcs_info-examples)
FORCE_RUN_VCS_INFO=1

+vi-pre-get-data() {
    # Only Git and Mercurial support and need caching. Abort if any other
    # VCS is used.
    [[ "$vcs" != git && "$vcs" != hg ]] && return

    # If the shell just started up or we changed directories (or for other
    # custom reasons) we must run vcs_info.
    if [[ -n $FORCE_RUN_VCS_INFO ]]; then
        FORCE_RUN_VCS_INFO=
        return
    fi

    # If we got to this point, running vcs_info was not forced, so now we
    # default to not running it and selectively choose when we want to run
    # it (ret=0 means run it, ret=1 means don't).
    ret=1
    # If a git/hg command was run then run vcs_info as the status might
    # need to be updated.
    case "$(fc -ln $(($HISTCMD-1)))" in
        git*)
            ret=0
            ;;
        hg*)
            ret=0
            ;;
    esac
}

# Must run vcs_info when changing directories.
prompt_chpwd() {
    FORCE_RUN_VCS_INFO=1
}

# a lot of this from https://github.com/wincent/wincent/blob/main/aspects/dotfiles/files/.zshrc
# # http://zsh.sourceforge.net/Doc/Release/User-Contributions.html
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr "%F{green}●%f" # default 'S'
zstyle ':vcs_info:*' unstagedstr "%F{red}●%f" # default 'U'
zstyle ':vcs_info:*' use-simple true
zstyle ':vcs_info:git+set-message:*' hooks git-untracked git-stashed
zstyle ':vcs_info:git*:*' formats '(%F{yellow}λ:%F{blue}%b%m%c%u%F{blue})%f ' # default ' (%s)-[%b]%c%u-'
zstyle ':vcs_info:git*:*' actionformats '(%F{yellow}λ:%F{blue}%b|%a%m%c%u%F{blue})%f ' # default ' (%s)-[%b|%a]%...c%u-'
zstyle ':vcs_info:*+pre-get-data:*' hooks pre-get-data # lazy update git info

function +vi-git-untracked() {
  emulate -L zsh
  if [[ -n $(git ls-files --exclude-standard --others 2> /dev/null) ]]; then
    hook_com[unstaged]+="%F{red}○%f"
  fi
}

function +vi-git-stashed() {
  emulate -L zsh
  if [[ -n $(git stash list 2> /dev/null) ]]; then
    hook_com[unstaged]+="%F{cyan}◆%f"
  fi
}

function prompt_precmd() {
  vcs_info
}

function set_prompt() {
  PROMPT='$prompt_newline%F{red}[20%D %*]$prompt_newline%F{cyan}${PWD/#$HOME/~} 〉%f'
}

function set_rprompt() {
  RPROMPT="${vcs_info_msg_0_}%F{cyan}[%m]%f"
}

autoload -U add-zsh-hook
add-zsh-hook precmd prompt_precmd
add-zsh-hook chpwd prompt_chpwd
add-zsh-hook precmd set_prompt
add-zsh-hook precmd set_rprompt
